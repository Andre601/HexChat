plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

group = 'com.andre601'
version = new Version(major: 1, minor: 5, revision: 2)

jar {
    sourceCompatibility = 1.8
}

repositories {
    mavenCentral()
    maven {url = 'http://repo.extendedclip.com/content/repositories/placeholderapi/'}
    maven{ url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven{ url = 'https://rayzr.dev/repo/' }
    maven{ url = 'https://repo.codemc.org/repository/maven-public/' }
}

dependencies {
    compileOnly(group: 'me.clip', name: 'placeholderapi', version: '2.10.9')
    compileOnly(group: 'org.spigotmc', name: 'spigot-api', version: '1.16.2-R0.1-SNAPSHOT')
    
    implementation(group: 'me.rayzr522', name: 'jsonmessage', version: '1.2.1')
    implementation(group: 'org.bstats', name: 'bstats-bukkit', version: '1.7')
    implementation(group: 'me.mattstudios.utils', name: 'matt-framework', version: '1.3')
    implementation(group: 'me.mattstudios.utils', name: 'matt-framework-msg', version: '1.1')
}

shadowJar{
    relocate('me.rayzr522', 'com.andre601.hexchat.dependencies.json')
    relocate('me.mattstudios.mf', 'com.andre601.hexchat.dependencies.command')
    relocate('me.mattstudios.mfmsg', 'com.andre601.hexchat.dependencies.message')
    relocate('org.bstats', 'com.andre601.hexchat.dependencies.metrics')
}

class Version{
    String major, minor, revision

    static String getBuild(){
        System.getenv("BUILD_NUMBER") ? "_" + System.getenv("BUILD_NUMBER") :
                System.getProperty("BUILD_NUMBER") ? "_" + System.getProperty("BUILD_NUMBER") : ""
    }

    String toString(){
        "$major.$minor.${revision}$build"
    }
}
